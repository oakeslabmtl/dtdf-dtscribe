vocabulary <https://bentleyjoakes.github.io/DTDF/vocab/DTDFVocab#> as DTDFVocab {
	
	extends <http://www.w3.org/2001/XMLSchema#> as xsd
	
	extends<https://bentleyjoakes.github.io/DTDF/vocab/base#> as base
	
	aspect Component < base:DescribedThing, base:ContainedElement, ImplementationThing
	
	// Set to true if the component can display a visualization
	scalar property HasVisualization [
		domain Component
		range xsd:boolean
		functional
	]

	// Implementation state
	concept Implementation < Component
	aspect ImplementationThing
	relation entity HasImplementation [
		from ImplementationThing
		to Implementation
		forward hasImplementation
	]
	
	aspect PurposeThing
	scalar property purpose [
		domain PurposeThing
		range xsd:string
	]
	
	aspect AutomaticEnabledThing
	scalar property IsAutomatic [
		domain AutomaticEnabledThing
		range xsd:boolean
		functional
	]
	
	//------------------------------
	// C0: Digital Twin
	
	concept DigitalTwin < base:ContainedElement, PurposeThing, MultiplicityThing, FidelityThing
	
	concept DTComponent < Component, FidelityThing [
		restricts all base:isContainedIn to DigitalTwin
	]
	
	//------------------------------
	// C1: SystemUnderStudy
	concept SystemUnderStudy < base:ContainedElement, PurposeThing
	relation entity HasSystem [
		from SystemUnderStudy
		to System
		forward hasSystem
	]
	relation entity HasEnvironment [
		from SystemUnderStudy
		to Environment
		forward hasEnvironment
	] 
	relation entity HasAgent [
		from SystemUnderStudy
		to Agent
		forward hasAgent
	]
	
	concept SystemComponent < Component
	concept ConnectionComponent < Component
	
	concept System < base:DescribedThing, base:ContainedElement, MultiplicityThing, PurposeThing
	concept Environment < base:DescribedThing, base:ContainedElement
	concept Agent < base:DescribedThing
	
	//------------------------------
	// C2: Acting Components
	concept ActingComponent < ConnectionComponent, TimeScaleThing

	//------------------------------
	// C3: Sensing Components
	// TODO: Align with SOSA ontology - https://www.w3.org/TR/vocab-ssn/#SOSASensor
	concept SensingComponent < ConnectionComponent, TimeScaleThing
	
	//------------------------------
	// C4: Physical to Virtual Interactions
	concept DataTransmitted < ConnectionComponent, TimeScaleThing, AutomaticEnabledThing
	relation entity ProducedData [
		from SensingComponent
		to DataTransmitted
		forward producedData
		reverse producedFrom
	]
	
	//------------------------------
	// C5: Virtual to Physical Interaction
	concept VirtualToPhysical < DTComponent, TimeScaleThing
	
	//------------------------------
	// C6: Services
	concept Service < DTComponent, TimeScaleThing
	
	//------------------------------
	// C7: TimeScale
	concept TimeScale < base:DescribedThing
	aspect TimeScaleThing
	relation entity HasTimeScale [
		from TimeScaleThing
		to TimeScale
		forward hasTimeScale
	]
	
	//------------------------------
	// C8: Multiplicity
	concept Multiplicity < base:DescribedThing
	aspect MultiplicityThing
	scalar property multiplicity [
		domain MultiplicityThing
		range xsd:integer
		functional
	]
	
	//------------------------------
	// C9: LifeCycleStage
	concept LifeCycleStage < base:DescribedThing
	relation entity AtStage [
		from Service
		to LifeCycleStage
		forward atStage
		reverse providedService
	]
	
	//------------------------------
	// C10: Models/Data
	aspect Input
	concept Model < DTComponent, Input
	concept Data < DTComponent, Input
	
	relation entity InputTo [
		from Input
		to Enabler
		forward inputTo
		reverse hasInput
	]
	
	// relations from sensor to data transmitted
	// relations from data transmitted to data on DT side
	relation entity DataInput [
		from DataTransmitted
		to Data
		forward asData
		reverse fromData
	]
	
	//------------------------------
	// C11: Enablers
	concept Enabler < DTComponent
	
	relation entity Enables [
		from Enabler
		to Service
		forward enables
		reverse enabledBy
	]
	
	scalar property IsCommEnabler [
		domain Enabler
		range xsd:boolean
		functional
	]
	
	//------------------------------
	// C12: Constellation
	// NOTE: Connections between DTComponents are specialized
	concept Constellation < base:DescribedThing
	
	//------------------------------
	// C13: EvolutionStage
	concept EvolutionStage < base:DescribedThing
	relation entity nextEvoStage [
		from EvolutionStage
		to EvolutionStage
		forward nextStage
		irreflexive
	]
	relation entity DTEvolution [
		from DigitalTwin
		to EvolutionStage
		forward evolution
	]
	
	//------------------------------
	// C14: FidelityConsideration
	concept FidelityConsideration < base:DescribedThing
	aspect FidelityThing
	relation entity DTFidelity [
		from FidelityThing
		to FidelityConsideration
		forward fidelityConsideration
	]
	
	//------------------------------
	// C15: TechnicalConnection
	concept TechnicalConnection < base:DescribedThing
	relation entity TechDetails [
		from DTComponent
		to TechnicalConnection
		forward techDetails
	]
	
	//------------------------------
	// C16: Hosting/Deployment
	concept Deployment < base:DescribedThing
	relation entity DeploymentDetails [
		from DTComponent
		to Deployment
		forward deploymentDetails
	]
	
	//------------------------------
	// C17: Insights/DecisionMaking
	
	aspect ProvidedThing < TimeScaleThing
	concept Insight < ConnectionComponent, ProvidedThing
	concept Action < ConnectionComponent, ProvidedThing, AutomaticEnabledThing
	
	relation entity Provides [
		from Service
		to ProvidedThing
		forward provides
		reverse providedBy
	]

	
	//------------------------------
	// C18: Horizontal Integration
	concept HoriIntegration < base:DescribedThing
	relation entity HoriIntegrationRelation [ 
		from DigitalTwin
		to HoriIntegration
		forward hIntegration
	]
	
	//------------------------------
	// C19: Data ownership and privacy	
	concept DataOwnershipPrivacy < base:DescribedThing
	
	//------------------------------
	// C20: Standardization
	concept Standardization < base:DescribedThing
	
	//------------------------------
	// C21: Security and safety considerations	
	concept SecuritySafety < base:DescribedThing
	
}